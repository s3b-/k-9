buildscript {
    ext {
        buildConfig = [
                'compileSdk': 29,
                'targetSdk': 28,
                'minSdk': 21,
                'buildTools': '29.0.2',
                'robolectricSdk': 28
        ]

        versions = [
                'kotlin': '1.3.61',
                'androidxAppCompat': '1.2.0-alpha02',
                'androidxRecyclerView': '1.1.0',
                'androidxLifecycle': '2.2.0',
                'androidxAnnotation': '1.1.0',
                'androidxNavigation': '2.2.1',
                'androidxConstraintLayout': '1.1.3',
                'androidxWorkManager': '2.3.1',
                'androidxFragment': '1.2.1',
                'androidxLocalBroadcastManager': '1.0.0',
                'androidxCore': '1.2.0',
                'materialComponents': '1.1.0',
                'preferencesFix': '1.1.0',
                'okio': '2.4.3',
                'moshi': '1.9.2',
                'timber': '4.5.1',
                'koin': '2.1.1',
                'commonsIo': '2.6',
                'mime4j': '0.8.1',
                'okhttp': '4.2.1',

                'androidxTestRunner': '1.1.1',
                'junit': '4.12',
                'robolectric': '4.2.1',
                'mockito': '3.2.4',
                'mockitoKotlin': '2.2.0',
                'truth': '1.0.1'
        ]

        javaVersion = JavaVersion.VERSION_1_8
        kotlinJvmVersion = "1.8"
    }

    repositories {
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.1.1"
    }
}

project.ext {
    testCoverage = project.hasProperty('testCoverage')
}

subprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    configurations.all {
        resolutionStrategy {
            // com.github.amlcurran.showcaseview:library depends on com.android.support:support-v4
            force "androidx.legacy:legacy-support-v4:1.0.0"
        }
    }
}
